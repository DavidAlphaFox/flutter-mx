(ns example.demo.isolate.ixp.ixp
  (:require
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]
    ["package:flutter/widgets.dart" :as w]
    ["dart:io" :as io]
    ["dart:convert" :as convert]
    [clojure.string :as str]
    [clojure.walk :as walk]
    [tilton.mx.util :as util]
    [tilton.mx.api :as mx
     :refer [dp dpx cI cF cF+ cF+n cFn cFonce mget mget? mset! mswap! minfo fasc fasc-inclusive
             fmu muv muiv fm* mav mx-bound? wmx-iso]]
    [tilton.fmx.api :as fx
     :refer [scaffold app-bar text center column hero material-app
             container row expanded icon padding opacity ignore-pointer
             checkbox-list-tile icon-button fx-render as-dart-callback]]
    [example.demo.isolate.common :as cmn
     :refer [clock-face slow-is-prime? dbgiso dbgisox say-delay]]
    [example.demo.isolate.ixp.iso-control
     :refer [on-off pause-resume kill ping]]
    ["dart:isolate" :as di
     :refer [Isolate SendPort Capability ReceivePort]]))

(defn just-slow-is-prime? [n]
  (dp :slow-is-prime-entry n
    :processors (when (not (fx/is-web?)) io/Platform.numberOfProcessors))
  (cond
    (= n 1) false
    :else (not (some (fn [divisor]
                       (zero? (mod n divisor)))
                 (range 2 n)))))

(defn decide-prime-in-main [n]
  (fx/elevated-button+icon
    {:onPressed (cF (as-dart-callback []
                      (mset! (fasc :app) :worker-result
                        {:msg-type :prime-decision
                         :n        n
                         :value    (just-slow-is-prime? n)})))
     :label     (m/Text (str "Main Prime!? " n))
     :icon      (m/Icon m/Icons.directions_run)}))

(defn decide-prime? [n]
  (fx/elevated-button+icon
    {:onPressed (cF (when-let [iso (mav :iso)]
                      (when-let [to-iso (mav :to-iso)]
                        (as-dart-callback []
                          (dbgiso :onpress-prime-tasking-iso (mav :iso))
                          (.send ^SendPort to-iso
                            {:msg-type :decide-prime?
                             :n        n})))))
     :label     (m/Text (str "Prime? " n))
     :icon      (m/Icon m/Icons.directions_run)}))

(defn slo-prime-worker-listener [to-main {:keys [msg-type n] :as task}]

  (cond
    (string? task)
    (slo-prime-worker-listener to-main (util/dart-map->cljd-map
                                         (convert/jsonDecode task)))
    :else (do (dp :slo-prime-w-sees task)
            (case msg-type
              :decide-prime?
              (.send ^SendPort to-main
                {:msg-type :prime-decision
                 :n        n
                 :value    (just-slow-is-prime? n)})
              "ping"
              (.send ^SendPort to-main
                {:msg-type :ack
                 :response (:response task)})

              (dp :worker-IGNORES-task task))))
  nil)

(defn slo-prime-worker [{:keys [msg-type ^SendPort to-main] :as start}]
  (let [from-main (ReceivePort)]
    (dp :worker-sends-its-receiver (.-sendPort from-main))
    (.send to-main {:msg-type :config
                    :to-iso   (.-sendPort from-main)})
    (dp :worker-starts-listening from-main)
    (.listen from-main (fn [msg]
                         (slo-prime-worker-listener to-main msg)))
    nil))

;;; --- make app ----------------------------------------

(defn make-app []
  (fx/material-app {}
    {:name          :app
     :on-exit       (cF+ [:watch (fn [_ me new _ _]
                                   (.listen ^ReceivePort new
                                     (fn [msg] (dp :on-exit!!!! msg))))]
                      (ReceivePort))
     :on-error      (cF+ [:watch (fn [_ me new _ _]
                                   (.listen ^ReceivePort new
                                     (fn [msg] (dp :on-error!!!! msg))))]
                      (ReceivePort))
     :start-paused? true
     :iso           (cF+ [:async? true
                          :watch (fn [_ me new-iso prior _]
                                   (when (dart/is? prior Isolate)
                                     (dbgisox :watch-killing-prior-iso! prior)
                                     (.kill ^Isolate prior))
                                   (when new-iso
                                     (wmx-iso
                                       (mset! me :resume-cap (.-pauseCapability ^Isolate new-iso))))
                                   (dbgiso :watch-sees-new-iso! new-iso))]
                      (when (mget (fm* :on-off) :value)
                        (Isolate.spawn slo-prime-worker
                          {:msg-type :config
                           :to-main  (.-sendPort ^ReceivePort (mget me :from-iso))}
                          .paused (mget me :start-paused?)
                          .onExit (.-sendPort ^ReceivePort (mget me :on-exit))
                          .onError (.-sendPort ^ReceivePort (mget me :on-error)))))
     :resume-cap    (cI nil :watch (fn [_ _ new prior _]
                                     (dp :resume-cap! new)))

     :iso-paused?   (cF+n [:watch (fn [_ me paused? prior _]
                                    (when-let [iso ^Isolate? (mget me :iso)]
                                      (when (mx-bound? prior)
                                        (cond
                                          paused? (let [resumer (.pause iso)]
                                                    (dp :watch-paused-resumer!! iso resumer)
                                                    (wmx-iso
                                                      (mset! me :resume-cap resumer)))
                                          :else (let [resumer (mget me :resume-cap)]
                                                  (dp :watch-paused-resuming!! resumer)
                                                  (.resume iso resumer))))))]
                      (mget me :start-paused?))
     :main-listener (cF+ []
                      (when-let [iso (mget me :iso)]
                        (when-let [from-iso (mget me :from-iso)]
                          (dp :main-listenng!!)
                          (.listen ^ReceivePort from-iso
                            (fn [{:keys [to-iso] :as result}]
                              (dp :main-hears!!! result)
                              (case (:msg-type result)
                                :config (wmx-iso
                                          (dp :main-sees-config!!!! result)
                                          (mset! me :to-iso (:to-iso result)))
                                (wmx-iso
                                  (dpx :forwarding-result result)
                                  (mset! me :worker-result result))))))))
     :worker-result (cI nil :watch (fn [_ _ new _ _]
                                     (when new (dp :worker-res new))))
     :from-iso      (cF (when (mget (fm* :on-off) :value)
                          (ReceivePort)))
     :to-iso        (cI nil)}
    (scaffold
      {:appBar (app-bar {:title           (fx/text "Isolate Explorer 23")
                         :backgroundColor m/Colors.pink.shade50})}
      (column                                               ;{:crossAxisAlignment m/CrossAxisAlignment.stretch}
        (clock-face)
        (row {:mainAxisAlignment m/MainAxisAlignment.spaceEvenly}
          (on-off)
          (pause-resume)
          (kill)
          (ping "Pong"))
        (decide-prime-in-main 9000041)
        (decide-prime? 9000039)
        (decide-prime? 9000041)
        (text {}
          (let [{:keys [msg-type n value] :as result} (mav :worker-result)]
            (case msg-type
              :prime-decision (str "N " n " is " (when-not value "NOT ") "prime.")
              :ack (:response result)
              "...")))))))

