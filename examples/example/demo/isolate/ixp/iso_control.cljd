(ns example.demo.isolate.ixp.iso-control
  (:require
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]
    ["package:flutter/widgets.dart" :as w]
    ["dart:convert" :as convert]
    [clojure.string :as str]
    [clojure.walk :as walk]
    [tilton.mx.api :as mx
     :refer [dp dpx cI cF cF+ cF+n cFn cFonce mget mget? mset! mswap! minfo fasc fasc-inclusive
             fmu muv fm* mav mx-bound? wmx-iso with-cc]]
    [tilton.fmx.api :as fx
     :refer [scaffold app-bar text center column hero material-app
             container row expanded icon padding opacity ignore-pointer
             checkbox-list-tile icon-button fx-render dart-cb]]

    ["dart:isolate" :as di
     :refer [Isolate SendPort ReceivePort]]
    [example.demo.isolate.common
     :refer [clock-face slow-is-prime? just-slow-is-prime?
             result-clear dbgiso iso-name say-delay]]))

(defn on-off []
  (fx/elevated-button+icon
    {:onPressed (dart-cb []
                  (mswap! me :value not))
     :label     (cF (m/Text (if (mget me :value) "Clear" "Spawn")))
     :icon      (cF (if (mget me :value) (m/Icon m/Icons.cancel) (m/Icon m/Icons.play_arrow)))}
    {:name  :on-off
     :value (cI true)}))

(defn start-paused []
  (fx/sized-box {:width 180}
    (checkbox-list-tile
      {:key             (m/ValueKey "start-paused")
       :controlAffinity m/ListTileControlAffinity.leading
       :title           (m/Text "Start Paused")
       :value           (cFn (mav :start-paused?))
       :onChanged       (cF (fn [new] (wmx-iso (mswap! me :value not))))}
      {:name :auto-play})))

(defn pause-resume []
  (fx/elevated-button+icon
    {:onPressed (cF (when-let [iso ^Isolate? (mav :iso)]
                      (dart-cb []
                        (mswap! (fasc :app) :iso-paused? not))))
     :label     (cF (m/Text (if (mav :iso-paused?) "Resume" "Pause")))
     :icon      (cF (m/Icon (if (mav :iso-paused?) m/Icons.play_arrow m/Icons.pause)))}))

(defn kill []
  (fx/elevated-button+icon
    {:onPressed (cF (when-let [iso (mav :iso)]
                      (dart-cb []
                        (dbgiso :onpress-killing-iso iso)
                        (.kill ^Isolate iso))))
     :label     (m/Text "Kill")
     :icon      (m/Icon m/Icons.cancel)}))

(defn ping [ping-text]
  (let [ping-id (atom -1)]
    (fx/elevated-button+icon
      {:onPressed (cF (dp :ping-sees (mav :iso) (mav :to-iso))
                    (when-let [iso (mav :iso)]
                      (when-let [to-iso (mav :to-iso)]
                        (dart-cb []
                          (dbgiso :onpress-pinging-iso iso)
                          (.ping ^Isolate iso to-iso
                            ; ping is fussier about what it will allow in a response
                            .response (convert/jsonEncode
                                        (walk/stringify-keys
                                          {:msg-type "ping"
                                           :response (str (swap! ping-id inc) ": " ping-text)}))
                            .priority Isolate.immediate)))))
       :label     (m/Text "Ping")
       :icon      (m/Icon m/Icons.network_ping)})))

;;; --- job starters -----------

(defn decide-prime-in-main [n]
  (fx/elevated-button+icon
    {:onPressed (cF (dart-cb []
                      (result-clear)
                      (doto (.-instance m/WidgetsBinding)
                        (.addPostFrameCallback
                          (fn [^Duration d]
                            (wmx-iso
                              (let [r (just-slow-is-prime? n)]
                                (mset! (fasc :app) :worker-result
                                  {:msg-type :prime-decision
                                   :run-id   (rand-int 9999)
                                   :n        n
                                   :value    r}))))))))
     :label     (m/Text (str "Main " n))
     :icon      (m/Icon m/Icons.directions_run)}))

(defn decide-prime? [n]
  (fx/elevated-button+icon
    {:onPressed (cF (when-let [iso (mav :iso)]
                      (when-let [to-iso (mav :to-iso)]
                        (dart-cb []
                          (result-clear)
                          (dbgiso :onpress-prime-tasking-iso (mav :iso))
                          (.send ^SendPort to-iso
                            {:msg-type :decide-prime?
                             :n        n})))))
     :label     (m/Text (str "Iso? " n))
     :icon      (m/Icon m/Icons.directions_run)}))

(defn run-prime? [n]
  (fx/elevated-button+icon
    {:onPressed (dart-cb []
                  (result-clear)
                  (mset! (fasc :app) :run-job
                    {:msg-type :decide-prime?
                     :run-id   (.-millisecondsSinceEpoch (.now DateTime))
                     :n        n}))
     :label     (m/Text (str "Iso.run " n))
     :icon      (m/Icon m/Icons.directions_run)}))

(defn run-enabled? []
  (fx/sized-box {:width 180}
    (checkbox-list-tile
      {:controlAffinity m/ListTileControlAffinity.leading
       :title           (m/Text "Auto-Run?")
       :value           (cI true)
       :onChanged       (cF #(wmx-iso (mset! me :value %)))}
      {:name :auto-run})))