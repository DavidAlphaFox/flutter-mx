(ns tiltontec.app.x02-two-counters-ala-flux
  (:require
    [clojure.string :as str]
    [tiltontec.util.base :refer [dprn dp dpx]]
    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.cell.core :as cell :refer [cF cFnil cI]]
    [tiltontec.model.base :refer [md-cell]]
    [tiltontec.model.core :refer [make mx-par mget md-kids cFkids fmu fmuv] :as md]
    [tiltontec.mx-flutter.tag :as tag]
    [tiltontec.mx-flutter.corex :as fxx]
    [tiltontec.mx-flutter.core :as fx
     :refer [ material-app center text row expanded elevated-button scaffold column]]
    ["package:flutter/widgets.dart"
     :refer [Text State StatefulWidget StatelessWidget]]
    ["package:flutter/material.dart" :as m
     :refer [MainAxisAlignment Colors Theme Icon Icons ThemeData runApp AppBar State]]
    ["package:flutter/painting.dart"
     :refer [TextStyle]]))

(defn build-counter [counter-key]
  (fxx/expanded
    (fxx/column
      {:mainAxisAlignment MainAxisAlignment/center}
      {:name  :counter-col
       :value (cF (let [app (md/fm-ascendant :counter-app me :must? true)]
                    (assert app "no find app")
                    (get-in (mget app :flux-store) [:counters counter-key])))}
      (fxx/text (str/capitalize (name counter-key)))
      (fxx/text
        {:style (TextStyle
                  :color m.Colors/cyan
                  :fontSize 32.0)}
        (str (md/fmuval :counter-col)))
      (fxx/elevated-button
        {:onPressed (tag/MXFXAsIs
                      #(md/mswap! (md/fm-ascendant :counter-app me)
                        :flux-store update-in [:counters counter-key] inc))}
        (fxx/text "+")))))

(defn make-app []
  (fxx/material-app
    {:title "Two Counters"
     :theme (cF (ThemeData :primarySwatch Colors/blue))}
    {:name       :counter-app
     ; simulating a separate global store, we bind a purely independent Matrix data structure that
     ; must be consulted for values. Pro tip: use `mswap! <the app> :flux-store update-in etc` to
     ; increment either counter.
     :flux-store (cI {:counters {:left 0 :right 0}})}

    (fxx/scaffold
      {:appBar (fxx/app-bar {:title (fxx/text "Two Counters ala Flux")})}
      (fxx/center
        (fxx/row
          {:mainAxisAlignment MainAxisAlignment/center}
          (mapv #(build-counter %) [:left :right]))))))