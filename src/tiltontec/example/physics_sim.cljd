(ns tiltontec.example.physics-sim
  (:require
    [tiltontec.util.core :as util]
    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.cell.core :as cell :refer [cF cFnil cI]]
    [tiltontec.model.core :refer [make mx-par mget md-kids cFkids fmu fmuv] :as md]
    [tiltontec.mx-flutter.core :as fx
     :refer [material-app center text grid-view-count row expanded elevated-button scaffold column]]
    ["package:flutter/widgets.dart"
     :refer [Text State StatefulWidget StatelessWidget] :as w]
    ["package:flutter/physics.dart" :as p]
    ["package:flutter/material.dart" :as m
     :refer [MainAxisAlignment MediaQuery Colors Theme Icon AnimationController Icons ThemeData runApp AppBar State]]
    ["package:flutter/painting.dart"
     :refer [TextStyle Alignment]]))

(defn logo-recoil [me ctx ^m/Offset px-per-s]
  (let [size (.size (MediaQuery/of ctx))
        dragger me
        controller (:controller (meta dragger))
        animation ^#/(m/Animation Alignment)
                  (.drive controller
                    (m/AlignmentTween
                      :begin (mget dragger :dragAlignment)
                      :end m.Alignment/center))
        units-per-s (m/Offset (/ (.dx px-per-s) (.width size))
                      (/ (.dy px-per-s) (.height size)))
        unit-velocity (.distance units-per-s)
        spring ^:const (p/SpringDescription
                         :mass 30
                         :stiffness 1
                         :damping 1)
        simulation (p/SpringSimulation spring 0 1 (- unit-velocity))]
    (doto controller
      .clearListeners
      (.addListener #(md/mset! dragger :dragAlignment (.value animation)))
      (.animateWith simulation))))

(defn draggable-card [& {:keys [drag-child]}]
  (fx/gesture-detector
    {:dragAlignment (cI m.Alignment/center)}
    {:name        :draggable-card
     :recoil      logo-recoil
     :initState   (fn [this me]
                    ;; we can use the meta of the widget for instance vars
                    (util/rmap-meta-setf [:controller me]
                      (m/AnimationController :vsync this)))
     :onPanDown   (fn [me ctx ^m/DragDownDetails details]   ;; (.stop controller)
                    (when-let [c (:controller (meta (md/fm-ascendant :draggable-card me)))]
                      (.stop c)))
     :onPanUpdate (fn [me ctx ^m/DragUpdateDetails details]
                    (let [size (.size (MediaQuery/of ctx))]
                      (md/mswap! me :dragAlignment ;; MX flow!!!!!!!!
                        #(.+ % (m/Alignment
                                 (/ (-> details .delta .dx) (/ (.width size) 2))
                                 (/ (-> details .delta .dy) (/ (.height size) 2)))))))
     :onPanEnd    (fn [me ctx ^m/DragEndDetails details]
                    (logo-recoil me ctx (-> details .velocity .pixelsPerSecond)))
     :dispose     (fn [this me]
                    (when-let [con (:controller (meta me))]
                      (.dispose con)))}
    (fx/align {:alignment (cF (mget (md/fm-ascendant :draggable-card me) :dragAlignment))}
      (fx/card drag-child))))

(defn make-app []
  (let [title "Physics Animation"]
    (material-app
      {:title title}
      (scaffold
        {:appBar (m/AppBar :title (m/Text title))}
        (draggable-card :drag-child (fx/flutter-logo {:size 128.0}))))))