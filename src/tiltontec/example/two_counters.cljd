(ns tiltontec.example.two-counters
  (:require
    [clojure.string :as str]
    [tiltontec.util.base :refer [dprn]]
    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.cell.core :as cell :refer [cF cFnil cI]]
    [tiltontec.model.base :refer [md-cell]]
    [tiltontec.model.core :refer [make mx-par mget  md-kids cFkids fmu fmuv] :as md]
    [tiltontec.mx-flutter.core :as fx
     :refer [render-fx material-app center text row expanded elevated-button text-ex scaffold column]]
    ["package:flutter/widgets.dart"
     :refer [Text State StatefulWidget StatelessWidget]]
    ["package:flutter/material.dart" :as m
     :refer [MainAxisAlignment Colors Theme Icon Icons ThemeData runApp AppBar State]]
    ["package:flutter/painting.dart"
     :refer [TextStyle]]))

(defn build-counter [counter-key]
  (expanded {} {}
    (column {:name  :counter-col
             :value (cF (let [app (md/fm-ascendant :counter-app me :me? false)]
                          (dprn :Column-checks-its-counter counter-key)
                          (get-in (mget app :flux-store) [:counters counter-key])))}
      {:mainAxisAlignment MainAxisAlignment/center}
      (text {} {} (str/capitalize (name counter-key)))
      (text {}
        {:style (TextStyle
                  :color Colors/green
                  :fontSize 48.0)}
        (do (dprn :building-counter!!!!!!!!!!!!! counter-key)
            (str (md/fmuval :counter-col))))
      (elevated-button {}
        {:onPressed (cFnil (fn ^void []
                             (dprn :incrementing-counter counter-key)
                             (md/mswap! (md/fm-ascendant :counter-app me)
                               :flux-store update-in [:counters counter-key] inc)))}
        (text {} {} "+")))))

(defn make-app []
  (material-app {:name       :counter-app
                 ; simulating a separate global store, we bind a purely independent Matrix data structure that
                 ; must be consulted for values. Pro tip: use `mswap! <the app> :flux-store update-in etc` to
                 ; increment either counter.
                 :flux-store (cI {:counters {:left 0 :right 0}})}
    {:title "Two Counters"
     :theme (cF (ThemeData :primarySwatch Colors/blue))}
    (scaffold {}
      {:appBar (AppBar :title (Text "Two Counters ala Flux"))}
      (center {} {}
        (row {}
          {:mainAxisAlignment MainAxisAlignment/center}
          (mapv #(build-counter %) [:left :right]))))))