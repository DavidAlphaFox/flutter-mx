(ns tiltontec.example.x01-counter
  (:require
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]
    [tiltontec.matrix.foundation :as mxf]
    [tiltontec.matrix.api :refer [dp cF cI cFn cFonce mpar mget mset! mswap! fm* cFkids minfo] :as mx]
    [tiltontec.flutter-mx.core :as fx
     :refer [as-dart-callback in-my-context as-content
             material-app scaffold app-bar floating-action-button
             hero center column text sized-box]]))

(defn make-app []
  (material-app
    {:title "Flutter/MX Counter Demo"
     :color m.Colors/cyan
     :theme (m/ThemeData .primarySwatch m.Colors/blue)}
    (scaffold
      {:appBar               (app-bar {:title (fx/text "Flutter/MX Counter")}
                               {:name :hi-appbar})
       :floatingActionButton (cF (floating-action-button
                                   {:onPressed (as-dart-callback []
                                                 (mswap! (fm* :z-counter) :value inc))
                                    :tooltip   "Increment"}
                                   (m/Icon m.Icons/add .color m.Colors/black)))}
      (center
        (column
          {:mainAxisAlignment m.MainAxisAlignment/center}
          (hero {:tag "imageHero"}
            (m/Image .image (m/AssetImage "image/kernighan.jpeg")
              .height 512))
          (sized-box {:height 16.0})
          (text {:style (p/TextStyle .color m.Colors/black
                          .fontSize 18.0)}
            "You clicked (+) this many times:")
          (sized-box {:height 16.0})
          (text
            {:style (in-my-context [me ctx]
                      (.-headline4 (.-textTheme (m.Theme/of ctx))))}
            {:name  :z-counter
             :value (cI 0)}
            (as-content
              (str (mx/my-value)))))))))
