(ns tiltontec.demo.todoMVC.core
  (:require
    [tiltontec.model.core :refer [mget mset! mset!x mswap! fasc mkids mpar] :as md]
    [tiltontec.cell.integrity :as integrity]
    [tiltontec.model.base :as mdbase]
    [tiltontec.cell.observer :refer [fn-obs]]
    [tiltontec.cell.core :refer [cF cF+ cF_ cI c_F cFonce]]
    [tiltontec.mx-flutter.core :as fx]
    [tiltontec.mx-flutter.store :as store]
    [tiltontec.demo.todoMVC.todo :as todo]
    [tiltontec.demo.todoMVC.input :as input]
    [tiltontec.demo.todoMVC.list-item :as item]
    [tiltontec.demo.todoMVC.footer :as footer]
    ["dart:convert" :as convert]
    ["package:localstore/localstore.dart" :as ls]
    ["package:flutter/widgets.dart" :as w]
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]
    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.util.base :refer [dprn dp dpx]]))

(declare todo-app-bar todo-controls my-fab )

(defn make-app []
  (let [title "todos"]
    (fx/material-app
      {:title                      title
       :theme                      (m/ThemeData
                                     .fontFamily "Helvetica Neue"
                                     .backgroundColor (.fromRGBO m/Color 245 245 245 1))
       :debugShowCheckedModeBanner false
       :debugShowMaterialGrid      false                    ;; but fun
       }
      {:name           :app
       :todo-db        (cI nil)
       :todo-db-loader (cF+ [:obs (fn [_ me td-lst-future _ tdlc]
                                    (.then ^#/(Future dynamic) td-lst-future
                                      (fn [tdl]
                                        (cty/with-fx-isolation
                                          (mset! me :todo-db tdl)))))]
                         ;; next returns an MX proxy of a to-do family, as a future
                         (todo/td-list-load "todo"))}
      (fx/scaffold
        {:appBar                  (todo-app-bar title)
         :backgroundColor         (.fromRGBO m/Color 245 245 245 1)
         :persistentFooterButtons [(fx/row
                                     {:mainAxisAlignment m.MainAxisAlignment/center}
                                     (fx/expanded {:flex 1} (footer/items-remaining-display))
                                     (fx/expanded {:flex 2} (footer/completion-filters))
                                     (fx/expanded {:flex 1} (footer/clear-completed-button)))]}
        (fx/ink {:color m.Colors/white}
          (fx/column
            (todo-controls)
            ; --- to-do list ------------------
            (fx/expanded
              ;; ^^^ required sizer to join column
              (fx/list-view+separated
                {:padding          ^:const (m.EdgeInsets/all 0.0)
                 :itemCount        (cF (count (todo/app-todos (fasc :app))))
                 :separatorBuilder (fx/as-is
                                     (fn [ctx i] (m/Divider .thickness 0.5)))
                 :itemBuilder      (cF (fx/as-is
                                         (fn [ctx i]
                                           ;; we have to yield a native Flutter widget ready for Flutter, so we "render",
                                           ;; aka convert FX proxy to real CLJD/Flutter widget
                                           (fx/fx-render ctx
                                             (nth (mget me :kids) i)))))}
                {}
                (map item/todo-list-item
                  (todo/app-todos (fasc :app)))))))))))

;;; todo credits in alert

(defn todo-app-bar [title]
  (fx/preferred-size
    {:preferredSize (.fromHeight m/Size 100)}
    (fx/app-bar {:backgroundColor (.fromRGBO m/Color 245 245 245 1)
                 :title           (fx/column
                                    {:crossAxisAlignment m.CrossAxisAlignment/end}
                                    (fx/text
                                      {:style (p/TextStyle
                                                .fontFamily "Helvetica Neue"
                                                .color (todo/title-red)
                                                .fontWeight m.FontWeight/w100
                                                .fontSize 64.0)}
                                      title))})))

(defn todo-controls []
  (fx/row
    ; --- they snuck a tool into the new-input area
    (fx/expanded {:flex 1}
      (input/todo-toggle-all))
    ; --- the input ----
    (fx/expanded {:flex 9}
      (fx/padding
        {:padding ^:const (m.EdgeInsets/all 16.0)}
        (input/todo-input)))))