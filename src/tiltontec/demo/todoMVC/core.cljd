(ns tiltontec.demo.todoMVC.core
  (:require
    ["dart:convert" :as convert]
    ["package:localstore/localstore.dart" :as ls]
    ["package:flutter/widgets.dart" :as w]
    ["package:flutter/material.dart" :as m]
    ["package:flutter/painting.dart" :as p]
    [tiltontec.model.core :refer [mget mset! mset!x mswap! fasc mkids mpar mkids] :as md]
    [tiltontec.cell.core :refer [cF cF+ cF_ cI c_F cFonce]]
    [tiltontec.mx-flutter.core :as fx]
    [tiltontec.demo.todoMVC.todo :refer [my-app] :as todo]
    [tiltontec.demo.todoMVC.input :as input]
    [tiltontec.demo.todoMVC.list-item :as item]
    [tiltontec.demo.todoMVC.footer :as footer]
    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.util.base :refer [dprn dp dpx]]))

(defn todo-app-bar [title]
  (fx/preferred-size
    {:preferredSize (.fromHeight m/Size 80)}
    (fx/app-bar {:backgroundColor (.fromRGBO m/Color 245 245 245 1)
                 :title           (fx/column
                                    {:crossAxisAlignment m.CrossAxisAlignment/end}
                                    (fx/text
                                      {:style (p/TextStyle
                                                .fontFamily "Helvetica Neue"
                                                .color (todo/title-red)
                                                .fontWeight m.FontWeight/w100
                                                .fontSize 64.0)}
                                      title))})))

(defn make-app []
  (let [title "todos"]
    (fx/material-app
      {:title                      title
       :theme                      (m/ThemeData
                                     .fontFamily "Helvetica Neue"
                                     .backgroundColor (.fromRGBO m/Color 245 245 245 1))
       :debugShowCheckedModeBanner false
       :debugShowMaterialGrid      false                    ;; but fun
       }
      {:name           :app
       :todo-list        (cI nil)
       :todo-list-loader (cF+ [:obs (fn [_ me load _ _]
                                    (.then ^#/(Future dynamic) load
                                      (fn [tdl]
                                        (cty/with-fx-isolation
                                          (mset! me :todo-list tdl)))))]
                         ;; next returns an MX proxy of a to-do family, as a future
                         (todo/ToDoList-load "todo"))}
      (fx/scaffold
        {:appBar                  (todo-app-bar title)
         :backgroundColor         (.fromRGBO m/Color 245 245 245 1)
         :persistentFooterButtons [(fx/row
                                     {:mainAxisAlignment m.MainAxisAlignment/center}
                                     (fx/expanded {:flex 1} (footer/items-remaining-display))
                                     (fx/expanded {:flex 2} (footer/completion-filters))
                                     (fx/expanded {:flex 1} (footer/clear-completed-button)))]}
        (fx/ink {:color m.Colors/white}
          (fx/column
            (input/todo-controls)
            ; --- to-do list ------------------
            (fx/expanded
              ;; ^^^ required sizer to join column
              (fx/list-view+separated
                {:padding          ^:const (m.EdgeInsets/all 0.0)
                 :itemCount        (cF (count (todo/app-todos (my-app))))
                 :separatorBuilder (fx/as-is
                                     (fn [ctx i] (m/Divider .thickness 0.5)))
                 :itemBuilder      (cF (fx/as-is
                                         (fn [ctx i]
                                           ;; we have to yield a native Flutter widget ready for Flutter, so we "render",
                                           ;; aka convert FX proxy to real CLJD/Flutter widget
                                           (fx/fx-render ctx
                                             (nth (mkids me) i)))))}
                {}
                (map item/todo-list-item
                  (todo/app-todos (my-app)))))))))))

;;; todo credits in alert



