(ns tiltontec.demo.todoMVC.todo
  (:require
    ["package:flutter/widgets.dart" :as w]
    ["package:flutter/material.dart" :as m]
    ["dart:convert" :as convert]
    ["package:localstore/localstore.dart" :as ls]
    [clojure.walk :as walk]

    [tiltontec.util.base :refer [dprn dp dpx]]
    [tiltontec.util.core :as util]
    [tiltontec.cell.base :refer [cinfo minfo] :as cty]
    [tiltontec.cell.core :refer [cF cFn cF+ cF_ cI c_F cFonce]]
    [tiltontec.model.core :refer [mget mpar mkids mname] :as md]
    [tiltontec.mx-flutter.store :as store]))

;;; --- should be in a utils file ----------------------
(defn title-red []
  (.fromRGBO m/Color 184 63 69 0.5))

(defmacro my-app []
  ;; fasc "captures" 'me
  `(tiltontec.model.core/fasc :app))

(defmacro my-todo-list []
  `(tiltontec.model.core/mget (tiltontec.model.core/fasc :app) :todo-list))

;;; --- handy accessors ---------------------------------

(defn td-ls-id [td] (mget td :ls-id))
(defn td-title [td]
  (assert td "td-title in td is nil")
  (mget td :title))
(defn td-completed [td] (mget td :completed))

;;; --- core LS functions re to-dos ----------------------

(defn td-initialize
  "Create a LS to-do from a CLJD map alone, returning LS id."
  [collection-name td-map]
  (assert (not (:ls-id td-map)) "td-map already written")   ;; already written
  (.ensureInitialized w/WidgetsFlutterBinding)
  (let [db (.-instance ls/Localstore)
        coll (.collection db collection-name)
        new-id (.-id (.doc coll))
        new-data (assoc td-map :ls-id new-id)]
    (-> coll
      (.doc new-id)
      (.set (walk/stringify-keys new-data)))
    new-id))

(defn td-rewrite
  "Update an LS to-do given only a CLJD map of its properties. Must include LS id."
  [collection-name td-map]
  (.ensureInitialized w/WidgetsFlutterBinding)
  (let [db (.-instance ls/Localstore)
        coll (.collection db collection-name)
        ls-id (:ls-id td-map)]
    (assert ls-id "td-rewrite map must include ls-id")
    (await (-> coll
             (.doc ls-id)
             (.set (walk/stringify-keys td-map))))
    ls-id))

(defn td-delete
  "Delete to-do data with given LS id from LS"
  [collection-name ls-id]
  (assert ls-id "td-delete requires ls-id")
  (.ensureInitialized w/WidgetsFlutterBinding)
  (let [db (.-instance ls/Localstore)
        coll (.collection db collection-name)]
    (await (-> coll
             (.doc ls-id)
             .delete))
    ls-id))

;;; --- FX-level to-dos and to-do list -----------------------------------------

(declare ToDo-update)

(deftype ToDo []
  :extends cty/Model
  cty/PObserver
  (observe [this slot me new-value prior-value cell]
    (when (and (td-ls-id me) (not= cty/unbound prior-value))
      (ToDo-update me))))

(defn make-ToDo
  ([td-lst title completed]
   (make-ToDo td-lst title completed nil))
  ([td-lst title completed ls-id]
   (md/make
     :par td-lst
     :mx-type (ToDo.)
     :ls-id (or ls-id
              (td-initialize "todo"
                {:title     title
                 :completed completed}))
     ;--
     :title (cI title)
     :completed (cI completed))))

(defn ToDo-update [td]
  (let [coll-name (mname (mpar td))]
    (td-rewrite coll-name (select-keys @td [:ls-id :title :completed]))))

;;; --- higher level ---------------------------

(deftype ToDoList []
  :extends cty/Model
  cty/PObserver
  (observe [this slot me new-value prior-value cell]
    (case slot
      :kids (when (not= cty/unbound prior-value)
              (let [lost (util/difference (set prior-value) (set new-value))]
                (await (doseq [td lost]
                         (dp :deleting=TD (td-ls-id td))
                         (await (td-delete "todo" (td-ls-id td))))))))))

(defn make-ToDoList
  ([name] (make-ToDoList name nil))
  ([name existing-todos]
   (md/make
     :mx-type (ToDoList.)
     :name name
     :kids (cFn (mapv (fn [xtd]
                        (let [{:keys [title completed ls-id]} xtd]
                          (make-ToDo me title completed ls-id)))
                  existing-todos)))))

(defn ToDoList-load
  "returns a future; await to get full-blown todos"
  [name]
  (make-ToDoList name
    (await (store/read-all-docs name))))

;;; --- even higher level ---------------------------------------

(defn app-todos [app]
  ;; we have to wait for async load of the to-do collection from localstorage
  (assert app "at no app")
  (assert (cty/md-ref? app))
  (when-let [td-lst (tiltontec.model.core/mget app :todo-list)]
    (assert (cty/md-ref? td-lst) (str "app-todos> td-lst not md-ref? " td-lst))
    (md/mkids td-lst)))