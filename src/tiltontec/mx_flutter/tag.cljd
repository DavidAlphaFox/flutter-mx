(ns tiltontec.mx-flutter.tag
  (:require ["package:flutter/material.dart" :as m
             :refer [MaterialApp Widget Scaffold MainAxisAlignment Colors Theme
                     FloatingActionButton Icon Icons ThemeData runApp AppBar State]]
            ["package:flutter/widgets.dart" :as w
             :refer [Text Center Expanded Column State PreferredSizeWidget StatefulWidget StatelessWidget]]
            ["package:flutter/painting.dart" :as p
             :refer [TextStyle]]
            [tiltontec.util.base :refer [dprn dp dpx]]
            [tiltontec.cell.base :refer [mx-type unbound Model PObserver observe md-ref?] :as cty]
            [tiltontec.util.core :as util
             :refer [atom? rmap-setf err rmap-meta-setf set-ify difference]]
            [tiltontec.model.core :refer [make mget md-kids mswap! cFkids] :as md]))

(defprotocol PDartWidget
  (parts [this] "eg, Scaffold [:appBar :body :floatingActionButton ...]")
  (parts-ex [this count] "Test")
  (builder [this])
  (fx-genner [this]))

(defn render-fx [fx]
  (cond
    (not (md-ref? fx))
    (do
     ;; (dprn :rnfx-sees-not-md-passing-thru fx)
      fx)

    :else (let [gen (mget fx :fx-gen)]
            (assert gen "OBAD: render-fx of model finds no fx-gen: ")
            (gen fx))))

(defn statefully [me builder]
  (reify :extends w/StatefulWidget
    (createState [this]
      (util/rmap-meta-setf [:state-ref me]
        (reify :extends w/State
          ^:mixin m/SingleTickerProviderStateMixin          ;; todo make specifiable (macroland?)
          (build [this ctx]
            (builder me ctx))
          (^void initState [this]
            (.initState ^super this)
            (when-let [init (mget me :initState)]
              (init this me)))
          (^void dispose [this]
            (when-let [orride (mget me :dispose)]
              (orride this me))
            (.dispose ^super this)))))))

(defn make-fx [mx-type fx-props custom-props cFkids-form]
  (apply tiltontec.model.core/make
    :mx-type mx-type
    :fx-prop-keys (keys fx-props)
    :kids cFkids-form
    :fx-gen (fn [me]
              ;; the default "statefully" works for must Flutter widgets,
              ;; but not all, such as AppBar, so we allow individual widget Types to
              ;; supply their own fx-gen.
              (if-let [type-fx-gen (fx-genner mx-type)]
                (type-fx-gen me)
                ; --- no special fx gen, so...the default:
                (tiltontec.mx-flutter.tag/statefully me
                  (fn [me ctx]
                    (dp :make-fx-fx-gen-hits-builder (cty/minfo me))
                    (let [builder (or (mget me :builder)
                                    (builder mx-type))]
                      (builder me ctx))))))
    (concat
      (vec (apply concat (seq fx-props)))
      (vec (apply concat (seq custom-props))))))

(defmacro deftag [tag fx-type-name]
  ;;(dprn :deftag-entry!!!!!!! tag fx-type-name)
  `(defmacro ~tag [& vargs#]
     ;;(dprn :expanding-deftag '~tag)
     (let [fx-type# '~fx-type-name]
       (cond
         (nil? vargs#)
         `(tiltontec.mx-flutter.tag/make-fx (new ~fx-type#)
            {} {} nil)

         (not (map? (first vargs#)))
         (do                                                ;;(dprn :just-kids!!!!!!!! '~tag)
           ;; we must have justkids
           `(tiltontec.mx-flutter.tag/make-fx (new ~fx-type#)
              {} {}
              (tiltontec.model.core/cFkids ~@vargs#)))

         (map? (second vargs#))
         ;; full spec, maybe kids
         (do                                                ;;(dprn :full-spec!!!!!!!!!!!! (second vargs#))
           `(tiltontec.mx-flutter.tag/make-fx (new ~fx-type#)
              ~(first vargs#)
              ~(second vargs#)
              ~(when-let [kids# (seq (nthrest vargs# 2))]
                 ;;(prn :kids-form-will-be kids#)
                 `(tiltontec.model.core/cFkids ~@kids#))))

         :else                                              ;; just first map of fx props and maybe kids
         `(tiltontec.mx-flutter.tag/make-fx (new ~fx-type#)
            ~(first vargs#)
            {}
            ~(when-let [kids# (seq (nthrest vargs# 1))]
               `(tiltontec.model.core/cFkids ~@kids#)))))))